#!/bin/sh

#	History
#	025	GLK	241214	br-1234... devices are a problem - we changed variable names but not device itself
#	024	GLK	241010	Do not fake WIP - if outside script sets it, we can use it - but to iffy to guess
#	023	GLK	240624	Provide media speed (FreeBSD/ifconfig for now), with duplex
#	022	GLK	240506	Only set IFINT if we have IFWIP - a public or separate NET as determined by IP/Mask
#	021	GLK	240501	Multiple IP issue - quote addresses & masks
#	020	GLK	240430	This is not working - fudging IFEXT - use IFTBD for unknown interface list (e.g. to pass to dhcpcd until it gets an assignment)
#	019	GLK	240429	Bug with IFINT being set erroneously with the real IFINT being down
#	018	GLK	240423	Respect /var/run/env/IF??? overrides set by services (dhcpcd etc)
#	017	GLK	240314	One less shell, use Gestalt hints for IFEXT if inside LAN firewall testing, fix IPcmp
#	016	GLK	240218	Remove non-bash-variable characters from interface name - FreeBSD borked with weird quoted wlan0
#	015	GLK	231110	Minor cleanup
#	014	GLK	231017	Support mask in IPcmp (8 bit chunks) in hex or decimal format
#	013	GLK	231015	Handle bridged devices (seeing 'master' in ip show addr output)
#	012	GLK	230804	Goodbye Jump. 169.254 should not be the main IP
#	011	GLK	230627	Missing virbr0 from virtual names
#	010	GLK	230530	Use loop type instead of 127.x for loopback IP check, aliases in IFxxIPA if more than one
#	009	GLK	230108	Support bridge members in MEM
#	008	GLK	221114	Recognize vmbrX for Proxmox
#	007	GLK	221011	Fib: on FreeBSD
#	006	GLK	220626	Docker or Linux bridging names interaces with 'br-' that can't be in variable names
#	005	GLK	220223	Virtual IPs in VIP instead of WIP/LIP
#	004	GLK	211213	New entry in ip show addr output - altname enp0s31f6 - ad as IFdevALT
#	003	GLK	210511	Take wifi if no ethernet is active for IFEXT
#	002	GLK	210511	Fix dual ext/int for int-first scans, separate IFLOC for loopback devices
#	001	GLK	210407	Add auto IP detection, fix some Wifi (assume "duplex" in media: line means ethernet)
#

IfProbe()
 {
	local	i v a

	# Read in Gestalt IF* hints
	for i in '/var/run/env/IF'???; do if [ -s "$i" ]; then
		read v < "$i"
		v="${v%% *}"; v="${v#* }"
		a="$a -v g${i##*/}=$v"
		# echo "g${i##*/}=\"$v\""
	fi; done

	{ command -v 'ip' >/dev/null && ip addr show || ifconfig -a; } | awk -v "gOS=$(uname -s)" $a '

		function Empty() {
			# Empty for each interface (not global for all)
			dName = ""
			dAddr = ""
			dAdIn = 0	# Address of current device is Internal (Private)
			dAlia = ""
			dMask = ""
			dAdd6 = ""
			dPfx6 = ""
			dMacA = ""
			dType = ""
			dMedi = ""
			dFast = ""
			dMemb = ""
			dActi = ""
			dStat = ""
			dFlag = ""
			dSSID = ""
			dMTU = ""
			dPID = ""
			dPwr = ""
			dAltN = ""
			dFib = ""
			dGrp = ""
			dBdg = ""
		 }

		function IPcmp(a, b, m, aa, bb) {
			split(a, aa, ".")
			split(b, bb, ".")
			if ("" == m)
				m = 32
			else if (substr(m, 1, 2) == "0x")
			 {
				m = 4 * (index(substr(m, 3), "0") - 1)	# Bits in mask e.g. 0xfff00000 is 12 (4 * (4 - 1))
				if (m < 0) m = 32
			 }
			for (i = 0; i < 4 && m >= 0; i++)
			 {
				if (aa[i] < bb[i])
					return -1
				else if (bb[i] < aa[i])
					return 1
				m = m - 8
			 }
			return 0
		 }

		function IfOut() {
			# Remove characters that would cause variables to fail
			dBash = dName
			gsub("-", "_", dBash)
			gsub(/[^[:alnum:]_]/, "", dBash)

			dAll = dAll == "" ? dBash : dAll " " dBash

			if ("Linux" == gOS && dBash ~ /^w/)	# Can not see a way to identify on Linux other than first character 
				dType = "wifi"

			ac=0
			if ("active" == dActi)
			 {
				dActive = dActive == "" ? dBash : dActive " " dBash
				ac=1
			 }
			else if ("running" == dActi)
			 {
				dRun = dRun == "" ? dBash : dRun " " dBash
				ac=1
			 }
			else if ("no" == dActi)
				dActi = ""

			# Address processing - dAdIn setting - 0 undefined, 1 autol, 2 private, <0 public
			if ("" == dAddr)
			 {
				if ("ether" == dType || "wifi" == dType || "bridge" == dType)
					dTbd = dTbd == "" ? dBash : dTbd " " dBash
			 }
			else
			 {
				print "IF" dBash "IP=\"" dAddr "\""
				if (dType == "tunnel" || dType == "virtual")
				 {
					dVip = (dVip == "" || dAddr == dVip) ? dAddr : dVip " " dAddr
				 }
				else if ("loop" != dType && "slave" != dType)
				 {
					if (dAddr ~ /^192.168.|^172.1[6-9]\.|^172.2[0-9]\.|^172.3[0-1]\.|^10\./)
					 {
						dAdIn = 2
						dLan = dLan == "" ? dBash : dLan " " dBash
						if (dLip == "" || dLip == dAddr)	# Same addr on another device?
							dLip = dAddr
						else
						 {
							c = IPcmp(dAddr, dLip, dMask)
							if (0 == c)	# Same LAN?
								dLip = dLip " " dAddr
							else if ((c > 1) || (dLip == gIFWIP))
							 {
								dWip = dLip		# Assume DMZ to this IP by convention - or Gestalt IFWIP
								dLip = dAddr
							 }
							# else if (dWip == "")
							# {
							#	dAdIn = -1	# Pretend public IP (DMZ)
							#	dWip = dAddr
							# }
						 }
					 }
					else if (dAddr ~ /^169.254\./)
					 {
						dAut = dAut == "" ? dBash : dAut " " dBash
						dAip = dAddr
						dAdIn = 1
					 }
					else if (dAddr != "" && dAddr != "0.0.0.0" )
					 {
						dAdIn = -1	# Public IP
						dWan = dWan == "" ? dBash : dWan " " dBash
						dWip = dAddr
					 }
				 }
			 }

			print "IF" dBash "DEV=" "\"" dName "\""
			if (dAdIn != 0) print "IF" dBash "INT=" dAdIn
			if (dMask != "") print "IF" dBash "MSK=\"" dMask "\""
			if (dAlia != "" && dAlia != dAddr) print "IF" dBash "IPA=\"" dAlia "\""
			if (dAdd6 != "") print "IF" dBash "IP6=\"" dAdd6 "\""
			if (dPfx6 != "") print "IF" dBash "PF6=\"" dPfx6 "\""
			if (dMacA != "") print "IF" dBash "MAC=" dMacA
			if (dType != "") print "IF" dBash "TYPE=" dType
			if (dMedi != "") print "IF" dBash "MED=" dMedi
			if (dMedi != "") print "IF" dBash "SPD=\"" dFast "\""
			if (dMemb != "") print "IF" dBash "MEM=\"" dMemb "\""
			if (dActi != "") print "IF" dBash "ACT=" dActi
			if (dStat != "") print "IF" dBash "UP=" dStat
			if (dFlag != "") print "IF" dBash "FLAG=\"" dFlag "\""
			if (dMTU != "") print "IF" dBash "MTU=" dMTU
			if (dPID != "") print "IF" dBash "PID=" dPID
			if (dSSID != "") print "IF" dBash "SSID=" dSSID
			if (dPwr != "") print "IF" dBash "PWR=" dPwr
			if (dAltN != "") print "IF" dBash "ALT=" dAltN
			if (dFib != "") print "IF" dBash "FIB=" dFib
			if (dGrp != "") print "IF" dBash "GRP=" dGrp
			if (dBdg != "") print "IF" dBash "BDG=" dBdg

			# Switch is a gnu awk extension
			if ("tunnel" == dType)
				dTun = dTun == "" ? dBash : dTun " " dBash
			else if ("loop" == dType)		# Localhost
				dLoc = dLoc == "" ? dBash : dLoc " " dBash
			else if ("slave" == dType)		# Localhost
				dSlave = dSlave == "" ? dBash : dSlave " " dBash
			else if ("bridge" == dType)
				dMstr = dMstr == "" ? dBash : dMstr " " dBash
			else if (dBash ~ /^awdl/ || dBash ~ /^llw/)		# Apple Wireless Direct / Low Latency Wireless
				dOther = dOther == "" ? dBash : dOther " " dBash
			else if ("wifi" == dType)
				dWifi = dWifi == "" ? dBash : dWifi " " dBash
			else if ("virtual" == dType)
				dVir = dVir == "" ? dBash : dVir " " dBash
			else if ("ether" == dType || "bridge" == dType)
			 {
				dEth = dEth == "" ? dBash : dEth " " dBash
				if ("ether" == dType)
				 {
					++ dECnt
					# Really, we should see if this IP is on existing IP network and not consider gateway if so?
					dGate = dGate + ac
				 }
				# print "NOTE=\"** IN: " dAdIn " : " ac " : " dInt " : " dExt " : " dAddr ":" dBash "\""
				if (ac > 0)
				 {
					# Logic should be if Pub - Ext = Pub, If one Ext = One, If two priv Ext = lower IP (convention only - affects this stanza and dLip above)
					dAEth = dAEth == "" ? dBash : dAEth " " dBash
					if (!dAdIn)
					 {
						e = "NoIPbutActive=" dBash
						dErr = dErr == "" ? e : dErr ":" e
						print "IF" dBash "ERR=NoIP"
					 }
				 }
			 }
			else
				dOther = (dOther == "") ? dBash : dOther " " dBash

			Empty()
		 }

		BEGIN {
			dErr = ""
			dAll = ""
			dAut = ""
			dAip = ""
			dWifi = ""
			dEth = ""
			dAEth = ""
			dOther = ""
			dSlave = ""
			dMstr = ""
			dActive = ""
			dRun = ""
			dLoc = ""
			dLan = ""	# Devices with private IPs only
			dWan = ""	# Devices with public IPs
			dLip = ""	# Local IPs in use
			dVip = ""	# Virtual IPs in use
			dWip = ""	# Public IPs in use
			dInt = ""	# Device is up, we have dExt with public or > IP address and this has dLip / private IP
			dExt = ""	# Device is up and assigned an IP (Public if == dWan, Private if dLan
			dTbd = ""	# Can not determine dExt - drop all non virtual / local devices in here for dhcpcd
			dVir = ""	# Virtual devices
			dTun = ""	# Tunnel devices
			dGIP = ""	# Not sure
			dECnt = 0
			dGate = 0
			Empty();
		 }

		// {
			# Are we starting a new device? E.g. en0: .* mtu .*
			if ($1 ~ /.*:/ && index($1, ":") == index($0, ":"))
			 {
				# print "*** NEW DEVICE: " $0
				if (dName != "")
					IfOut()
				dName = 1
				if ($2 ~ /.*:/)
					dName = 2
				dName = substr($dName, 1, length($dName) - 1)
				if (left = index($0, "<"))
				 {
					right = index($0, ">")
					split(substr($0, left + 1, right - left - 1), a, ",")
					for (f in a)
					 {
						dFlag = (dFlag == "") ? a[f] : dFlag " " a[f]

						if ("NO-CARRIER" == a[f])
							dActi = "no"
						else if ("LOOPBACK" == a[f])
							dType = "loop"
						else if ("POINTOPOINT" == a[f])
							dType = "tunnel"

						if ("no" != dActi && ("RUNNING" == a[f] || "LOWER_UP" == a[f]))
							dActi = "running"
					 }	
					# mtu 1500 qdisc pfifo_fast master br0 state UP group default qlen 1000 (example)
					split(substr($0, right), a, " ")
					for (f in a)
					 {
						if ("mtu" == a[f])
							dMTU = a[1 + f]
						else if ("master" == a[f])
						 {
							# dBdg = dBdg == "" ? a[f + 1] : dBdg " " a[f + 1]
							dType = "slave"
						 }
					 }
				 }

				if ((atLoc = index(dName, "@")) > 0)
					dName = substr(dName, 0, atLoc - 1)

				if ((gsub("\\.", "_", dName) > 0) || (index(dName, "docker") > 0) || (index(dName, "lxcbr") > 0) || (index(dName, "fwbr") > 0) || (index(dName, "virbr") > 0) || (atLoc > 0))
					dType = "virtual"
				else if (((atLoc = index(dName, "-")) > 0) || (1 == index(dName, "vmbr")) || (1 == index(dName, "br")))
				 {
					dType = "bridge"
					dBdg = dBdg == "" ? dName : dBdg "" dName
				 }

				if ($3 == "mtu")
					dMTU = $4
			   }
			else if ("inet" == $1)
			 {
				if ($2 !~ /^169.254\./)
				 {
					dAddr = dAddr == "" ? $2 : dAddr " " $2
					dMask = dMask == "" ? $4 : dMask " " $4
					dCast = $6
					split(dAddr, a, "/")
					dAddr=a[1]
					if (a[2] != "") dMask = a[2]
					if (dActi == "running") dActi = "active"
				 }
				dAlia = dAlia == "" ? $2 : dAlia " " $2
			 }
			else if ("inet6" == $1)
			 {
				d6 = $2
				p6 = $4
				split(d6, a, "/")
				d6=a[1]
				if (a[2] != "") p6=a[2]
				split(d6, a, "%")
				d6=a[1]
				dAdd6 = dAdd6 == "" ? d6 : dAdd6 " " d6
				dPfx6 = dPfx6 == "" ? p6 : dPfx6 " " p6
				if (dActi == "running") dActi = "active"
			 }
			else if ($1 ~ /link\/.*/)
			 {
				split($1, a, "/")
				if (dType == "")
					dType = a[2]
				dMacA = $2
			 }
			else if ($1 ~ /^options=.*/)
			 {
				# RXCSUM indicates hardware line (darwin)?
				if (dType == "")
				 {
					if ($1 ~ /.*RX.*/)
						dType = "ether"
					else if (dMTU == 1500)
						dType = "wifi"
				 }
			 }
			else if ($1 ~ /^capabilities=.*/)
			 {
				# CSUM indicates hardware line (BSD)?
				if (dType == "")
				 {
					if ($1 ~ /.*CSUM.*/)
						dType = "ether"
					else if (dMTU == 1500)
						dType = "wifi"
				 }
			 }
			else if ("ether" == $1 || "address:" == $1)
				dMacA = $2
			else if ("status:" == $1)
				dActi = $2
			else if ("Opened" == $1)
				dPID = $4
			else if ("media:" == $1)
			 {
				# Darwin: media: autoselect (1000baseT <full-duplex>)
				# FreeBSD: media: Ethernet autoselect (1000baseT <full-duplex>)
				dMedi = $2
				lp = index($0, "(")
				rp = index($0, ")")
				if (rp > lp) dFast = substr($0, lp + 1, rp - lp - 1)
				if (dMedi == "Ethernet" || $0 ~ /.*-duplex.*/)	# Not on Darwin  || dMedi == "autoselect")
					dType = "ether"
				if (dMedi ~ /.*unknown.*/)
					dMedi = "unknown"
			 }
			else if ($1 == "nd6" || $1 == "valid_lft" || $1 == "device" || $1 == "loop" || $1 == "TX" || $1 == "RX" || $1 ~ /^ec_capabilities=/ || $1 ~ /^enabled=/ || $1 ~ /^ec_enabled=/)
				dLast = $1
			else if ("ssid" == $1)
				dSSID = $2
			else if ("powersave" == $1)
				dPwr = $2
			else if ("altname" == $1)
				dAltN = $2
			else if ("fib:" == $1)
				dFib = $2
			else if ("groups:" == $1)
			 {
				dGrp = $2
				if ("wg" == dGrp)
					dType = "tunnel"
				else if ("lo" == dGrp)
					dType = "loop"
			 }
			else if ("member:" == $1)
				dMemb= ("" == dMemb) ? $2 : dMemb " " $2
			else
				print "IF" dName "_UNKNOWN=" "\"" $0 "\""
		 }

		END {
			if (dName != "")
				IfOut();

			# /var/run/env has overrides (put there by e.g. dhcpcd et al) - this should be taken care of above and may be redundant
			if (gIFEXT != "")
				dExt = gIFEXT
			else if (dWan != "")
				dExt = dWan
			else if (dLan != "")
				dExt = dLan

			if (dExt != "")
			 {
				if (gIFINT != "")
					dInt = gIFINT
				else if (dWip != "")
					dInt = dLan
			 }

			print "IFSUP=:"							# Use $IFSUP . $0 to only run me once
			print "IFSUPV=1.3.0.2024.06.24"			# Version of me
			print "IFALL=\"" dAll "\""				# All Devices
			print "IFAUTO=\"" dAut "\""				# Auto Config Devices
			print "IFAIP=\"" dAip "\""				# Auto Config IPs
			print "IFACT=\"" dActive "\""			# Configured Devices
			print "IFRUN=\"" dRun "\""				# Connected Unconfigured Devices
			print "IFWIFI=\"" dWifi "\""			# Wifi Devices
			print "IFETH=\"" dEth "\""				# Ethernet Devices
			print "IFATH=\"" dAEth "\""				# Active Ethernet
			print "IFTUN=\"" dTun "\""				# Tunnel Devices
			print "IFETC=\"" dOther "\""			# WTF
			print "IFSLV=\"" dSlave "\""			# Slave devices (no explicit dhcp e.g.)
			print "IFMST=\"" dMstr "\""				# Master devices - own the prior gnomes
			print "IFLOC=\"" dLoc "\""				# Localhost / Loopback
			print "IFLAN=\"" dLan "\""				# Private IP devices
			print "IFWAN=\"" dWan "\""				# Public IP devices
			print "IFLIP=\"" dLip "\""				# Private IP devices
			print "IFVIP=\"" dVip "\""				# Virtual (Container) IP devices
			print "IFWIP=\"" dWip "\""				# Public IP devices
			print "IFINT=\"" dInt "\""				# Internal IP ethernet
			print "IFEXT=\"" dExt "\""				# External IP ethernet
			print "IFTBD=\"" dTbd "\""				# No determinable assigned interface? List for dhcpcd
			print "IFVIR=\"" dVir "\""				# Virtual Interface (docker etc.)
			print "IFENUM=" dECnt					# Ethernet Device count
			print "IFGATE=" (dGate >= 2)			# Two or more connected ethernet ports?

			if (1>2) print "IFV() \n {\n\teval IFQ=\\$IF$1$2\n }\n"	# Example: IFV lo0 IP; echo $IFQ
			if (dErr != "")
				print "IFSUPERR=" dErr
		}'
 }

IfProbe
